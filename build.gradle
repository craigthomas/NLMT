buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.kt3k.coveralls'

version = '0.1.1'
group = 'com.github.craigthomas'
description = 'Natural Language Modeling Toolkit'

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        java {
            srcDir 'main/java'
        }
    }
    test {
        java {
            srcDir 'main/test'
        }
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'org.apache.commons:commons-math3:3.5'
    compile 'commons-io:commons-io:2.4'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    dryRun = false
    pkg {
        repo = 'maven'
        name = rootProject.name
        desc = project.description
        websiteUrl = 'https://github.com/craigthomas/NLMT'
        issueTrackerUrl = 'https://github.com/craigthomas/NLMT/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/craigthomas/NLMT.git'
        publish = true
        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = 'v0.1.1'
        }
    }
}

publishing {
    publications {
        nlmt1(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
            version project.version
        }
    }
}
